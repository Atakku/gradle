// Copyright 2023 Atakku <https://atakku.dev>

buildscript {
  repositories {
    mavenCentral()
    maven {
      name = 'Fabric'
      url = 'https://maven.fabricmc.net/'
    }
  }
  dependencies {
    classpath 'fabric-loom:fabric-loom.gradle.plugin:1.3-SNAPSHOT'
  }
}

apply plugin: net.fabricmc.loom.bootstrap.LoomGradlePluginBootstrap
apply plugin: 'maven-publish'

base {
  archivesName = project.mod_name
}
group = project.mod_group
version = "$project.mod_version+$project.minecraft_version"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile).configureEach {
  it.options.release = 17
}

sourceSets {
  // Shared Minecraft & Fabric Sourceset
  main {
    java.srcDirs = []
    resources.srcDirs = []
  }
}
jar.enabled = false
remapJar.enabled = false

// Configure a sourceset for each target
targets.each { target, deps ->
  sourceSets {
    "$target" {
      java { 
        srcDirs = ["java/"]
        include "**/$mod_id/$target/**"
      }
      resources.srcDirs = ["res/$target/"]
      compileClasspath += sourceSets.main.compileClasspath
      runtimeClasspath += sourceSets.main.runtimeClasspath
      deps.each { dep, include ->
        if (include) {
          compileClasspath += sourceSets."$dep".compileClasspath
          runtimeClasspath += sourceSets."$dep".runtimeClasspath
        }
      }
    }
  }

  loom.createRemapConfigurations sourceSets."$target"

  tasks.register("jar$target", Jar) {
    destinationDirectory = jar.destinationDirectory
    archiveClassifier = "dev$target"
    deps.each { dep, include ->
      if (include) {
        from sourceSets."$dep".output
      }
    }
    from sourceSets."$target".output
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
  }
  
  tasks.register("remapJar$target", net.fabricmc.loom.task.RemapJarTask) {
    archiveClassifier = "$target"
    dependsOn "jar$target"
    inputFile = tasks."jar$target".archiveFile
    classpath.from sourceSets."$target".compileClasspath
    addNestedDependencies = false
  }

  assemble.dependsOn "remapJar$target"

  dependencies {
    if (project.hasProperty('lombok_version')) {
      "${target}CompileOnly" "org.projectlombok:lombok:$project.lombok_version"
      "${target}AnnotationProcessor" "org.projectlombok:lombok:$project.lombok_version"
    }
    "${target}Implementation" sourceSets.main.output
    deps.each { dep, include ->
      "${target}Implementation" sourceSets."$dep".output
    }
  }

  publishing {
    publications {
      "$target"(MavenPublication) {
        from components.java
        artifactId = "$project.mod_id-$target-$project.minecraft_version"
      }
    }
  }
}

loom {
  runs {
    client {
      source = sourceSets.client
      name = "$project.mod_name Client"
      programArgs '--username', 'AtakkuDev'
      programArgs '--server', 'localhost'
      runDir = '.run/client'
    }
    server {
      source = sourceSets.server
      name = "$project.mod_name Server"
      runDir = '.run/server'
    }
  }
}


repositories {
  exclusiveContent {
    forRepository {
      maven {
        name = 'modrinth'
        url = 'https://api.modrinth.com/maven'
      }
    }
    filter {
      includeGroup 'maven.modrinth'
    }
  }
  maven {
    name = 'atakku'
    url = 'https://maven.atakku.dev/eufonia'
    credentials(PasswordCredentials)
    authentication {
      basic(BasicAuthentication)
    }
  }
  maven { 
    name = 'wisp'
    url = 'https://maven.wispforest.io' 
  }
  maven { 
    name = 'terraformersmc'
    url = "https://maven.terraformersmc.com/releases/"
  }
}

//if (project.name != 'shared' && rootProject.hasProperty('owo_version')) {
//    sourceSets.main.java.srcDirs += [ 'genJava/' ]
//    compileJava.options.generatedSourceOutputDirectory = file("$projectDir/genJava/dev/atakku/$mod_id/$project.name")
//}

dependencies {
  minecraft "com.mojang:minecraft:$project.minecraft_version"
  mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
  modImplementation "net.fabricmc:fabric-loader:$project.loader_version"
  modImplementation "net.fabricmc.fabric-api:fabric-api:$project.fabric_version"

//  fullImplementation sourceSets.main.output

  //if (project.hasProperty('owo_version')) {
  //  modImplementation "io.wispforest:owo-lib:$project.owo_version"
  //  annotationProcessor "io.wispforest:owo-lib:$project.owo_version"
  //  if (project.hasProperty('run_modmenu')) {
  //    modClientApi "com.terraformersmc:modmenu:$project.run_modmenu"
  //  }
  //}

  project.properties.each {
    if (it.key.startsWith("run_")) {
      modLocalRuntime "maven.modrinth:${it.key.split("_")[1]}:${it.value}"
    }
  }
}

tasks.withType(ProcessResources).configureEach {
  it.inputs.properties(project.properties.findAll { it.value instanceof String })
  it.filesMatching('fabric.mod.json') {
    expand(project.properties)
  }
}

publishing {
  repositories {
    maven {
      name = "atakku"
      url = "https://maven.atakku.dev/eufonia"
      credentials(PasswordCredentials)
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
}
